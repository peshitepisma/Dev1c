
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Заголовок = НСтр("ru = 'Сканирование штрихкода'");
	
	КодАкцизнойМарки = Параметры.КодАкцизнойМарки;
	НоменклатураЕГАИС = Параметры.НоменклатураЕГАИС;
	
	ИспользоватьАкцизныеМарки = Истина;
	ПравоРегистрацииШтрихкодовНоменклатурыДоступно = ШтрихкодированиеНоменклатурыЕГАИСПереопределяемый.ЕстьПравоРегистрацииШтрихкодовНоменклатуры();
	
	Если ЗначениеЗаполнено(НоменклатураЕГАИС) Тогда
		Элементы.ПоясняющийТекст.Заголовок = СтрШаблон(НСтр("ru = 'Алкогольная продукция ""%1""
		                                                          |не сопоставлена с номенклатурой предприятия. Отсканируйте штрихкод алкогольной продукции.'"), НоменклатураЕГАИС);
	Иначе
		Элементы.ПоясняющийТекст.Заголовок = НСтр("ru = 'По считанной акцизной марке не удалось определить алкогольную продукцию.
		                                                |Отсканируйте штрихкод алкогольной продукции.'");
	КонецЕсли;
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(
		ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещенияОбработаныНеизвестныеШтрихкоды(
		Новый ОписаниеОповещения("Подключаемый_ОбработаныНеизвестныеШтрихкоды", ЭтотОбъект),
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	ОповещенияПриЗавершении = СобытияФормЕГАИСКлиент.СтруктураОповещенийВнешнегоСобытия(ЭтотОбъект);
	
	СобытияФормЕГАИСКлиент.ВнешнееСобытиеПолученыШтрихкоды(ОповещенияПриЗавершении, ЭтотОбъект, Источник, Событие, Данные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Если Не ЗначениеЗаполнено(Штрихкод) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не заполнено поле ""Штрихкод""'"),,"Штрихкод");
		Возврат;
	КонецЕсли;
	
	ДанныеШтрихкодов = Новый Массив;
	ДанныеШтрихкодов.Добавить(Новый Структура("Штрихкод, Количество", Штрихкод, 1));
	Подключаемый_ПолученыШтрихкоды(ДанныеШтрихкодов, Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АкцизныеМарки

&НаКлиенте
Процедура Подключаемый_ПолученаАкцизнаяМарка(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		СтрШаблон(НСтр("ru = 'Считанный штрихкод %1 является кодом акцизной марки.
		                     |Считайте штрихкод EAN алкогольной продукции'"), Результат.КодАкцизнойМарки));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолученDataMatrix(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		СтрШаблон(НСтр("ru = 'Считанный штрихкод %1 является кодом акцизной марки.
		                     |Считайте штрихкод EAN алкогольной продукции'"), Результат.Штрихкод));
	
КонецПроцедуры

#КонецОбласти

#Область Штрихкоды

&НаКлиенте
Процедура Подключаемый_ПолученыШтрихкоды(ДанныеШтрихкодов, ДополнительныеПараметры) Экспорт
	
	Штрихкод = ДанныеШтрихкодов[0].Штрихкод;
	
	СопоставленнаяНоменклатура = ПолучитьДанныеПоШтрихкоду(Штрихкод);
	Если ЗначениеЗаполнено(СопоставленнаяНоменклатура) Тогда
		
		ПодключитьОбработчикОжидания("ЗакрытьФормуПриСканировании", 0.1, Истина);
		
	Иначе
		
		Если ПравоРегистрацииШтрихкодовНоменклатурыДоступно Тогда
			
			ШтрихкодированиеНоменклатурыЕГАИСКлиентПереопределяемый.ОткрытьФормуРегистрацииШтрихкодовНоменклатуры(ЭтотОбъект, ДанныеШтрихкодов);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Номенклатура со штрихкодом %1 не найдена'"), Штрихкод));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработаныНеизвестныеШтрихкоды(ДанныеШтрихкодов, ДополнительныеПараметры) Экспорт
	
	Подключаемый_ПолученыШтрихкоды(ДанныеШтрихкодов, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область Оборудование

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ЗакрытьФормуПриСканировании()
	
	Результат = АкцизныеМаркиКлиентСервер.РезультатОбработкиШтрихкодаАкцизнойМарки(КодАкцизнойМарки);
	ЗаполнитьЗначенияСвойств(Результат, СопоставленнаяНоменклатура);
	Результат.АлкогольнаяПродукция = НоменклатураЕГАИС;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоШтрихкоду(Штрихкод)
	
	Возврат ШтрихкодированиеНоменклатурыЕГАИСПереопределяемый.НайтиПоШтрихкоду(Штрихкод);
	
КонецФункции

#КонецОбласти

#КонецОбласти
