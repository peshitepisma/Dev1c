
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Элементы.ДатаИзготовления.Формат               = Параметры.ФорматДаты;
	Элементы.ДатаИзготовления.ФорматРедактирования = Параметры.ФорматДаты;
	
	ГоденДо = Параметры.ГоденДо;
	
	Если Не ЗначениеЗаполнено(ГоденДо) Тогда
		ГоденДо = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	
	СрокГодности                  = Параметры.СрокГодности;
	ЕдиницаИзмеренияСрокаГодности = Параметры.ЕдиницаИзмеренияСрокаГодности;
	
	Элементы.ГоденДо.ВыделенныеДаты.Очистить();
	Элементы.ГоденДо.ВыделенныеДаты.Добавить(ГоденДо);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГоденДоВыбор(Элемент, ВыбраннаяДата)
	Закрыть(ГоденДо);
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзготовленияПриИзменении(Элемент)
	РасчитатьГоденДо();
КонецПроцедуры

&НаКлиенте
Процедура СрокГодностиПриИзменении(Элемент)
	РасчитатьГоденДо();
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияСрокаГодностиПриИзменении(Элемент)
	РасчитатьГоденДо();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(ГоденДо);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура РасчитатьГоденДо()
	Если Не ЗначениеЗаполнено(ДатаИзготовления)
		Или Не ЗначениеЗаполнено(СрокГодности) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕдиницаИзмеренияСрокаГодности = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Год") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 31536000;
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Месяц") Тогда
		ГоденДо = ДобавитьМесяц(ДатаИзготовления, СрокГодности);
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Сутки") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 86400;
	ИначеЕсли ЕдиницаИзмеренияСрокаГодности =  ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Час") Тогда
		ГоденДо = ДатаИзготовления + СрокГодности * 3600;
	КонецЕсли;
	
	Элементы.ГоденДо.ВыделенныеДаты.Очистить();
	Элементы.ГоденДо.ВыделенныеДаты.Добавить(ГоденДо);
КонецПроцедуры

#КонецОбласти

#КонецОбласти
