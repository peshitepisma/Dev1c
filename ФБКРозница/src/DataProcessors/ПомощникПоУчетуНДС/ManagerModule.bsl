#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает информацию о количестве документов по учету НДС к обработке по организации за период.
//
// Параметры:
//	ПараметрыЗапроса - Структура вида "МассивОрганизаций, НачалоПериода, КонецПериода"
//	СтруктураВидимостиЭлементов - Структура, определяет - формировать ли запрос по документу. 
// 									См. ПолучитьСтруктуруВидимостиЭлементов()
//	ТолькоОбязательныеОперации - Булево, Отбирать для анализа только обязательыне операции (требуется для 
//									индикации состояния в помощнике закрытия месяца)
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица документов в обработке и уже сформированных (Документ, КоличествоКОформлению, КоличествоОформленных)
//
Функция СформироватьЗапросПоДокументамКОформлению(ПараметрыЗапроса, СтруктураВидимостиЭлементов = Неопределено, ТолькоОбязательныеОперации = Ложь) Экспорт
	
	Если СтруктураВидимостиЭлементов = Неопределено Тогда
		СтруктураВидимостиЭлементов = ВидимостьЭлементов(ПараметрыЗапроса.МассивОрганизаций, ПараметрыЗапроса.НачалоПериода);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ПараметрыЗапроса.КонецПериода = Дата('209901010000');
	КонецЕсли;
	
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПараметрыЗапроса.МассивОрганизаций);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
		МассивОрганизаций,
		Справочники.Организации.УправленческаяОрганизация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций",		МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода",			ПараметрыЗапроса.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",			ПараметрыЗапроса.КонецПериода);
	Запрос.УстановитьПараметр("КонецПериодаГраница",	Новый Граница(ПараметрыЗапроса.КонецПериода));
	
	ТекстыЗапросов = Новый Массив;
	
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураВыданный(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураВыданныйАванс(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураКомиссионеру(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
	ТекстыЗапросов.Добавить(ТекстЗапросаЗаявлениеОВвозеТоваров(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураНалоговыйАгент(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
	
	Если ТолькоОбязательныеОперации = Ложь Тогда
		
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураПолученный(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураПолученныйАванс(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактураКомитента(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
		ТекстыЗапросов.Добавить(ТекстЗапросаНДССостояниеРеализации0(СтруктураВидимостиЭлементов, ПараметрыЗапроса));
		
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СформироватьЗапросПоПрочимОперациям(ПараметрыЗапроса) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ПараметрыЗапроса.КонецПериода = Дата('209901010000');
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж КАК ЗаписьКнигиПродаж
	|ГДЕ
	|	ЗаписьКнигиПродаж.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаписьКнигиПродаж.Организация В (&МассивОрганизаций)
	|	И ЗаписьКнигиПродаж.Проведен
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.ЗаписьКнигиПокупок КАК ЗаписьКнигиПокупок
	|ГДЕ
	|	ЗаписьКнигиПокупок.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаписьКнигиПокупок.Организация В (&МассивОрганизаций)
	|	И ЗаписьКнигиПокупок.Проведен
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	РегистрСведений.СостоянияБлокировкиВычетаНДСПоСчетамФактурам.СрезПоследних(
	|		&КонецПериода, 
	|		Организация В (&МассивОрганизаций)) КАК СостоянияБлокировки 
	|ГДЕ
	|	СостоянияБлокировки.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБлокировкиВычетаНДС.Установлена)
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.СписаниеНДСнаРасходы КАК СписаниеНДСнаРасходы
	|ГДЕ
	|	СписаниеНДСнаРасходы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СписаниеНДСнаРасходы.Организация В (&МассивОрганизаций)
	|	И СписаниеНДСнаРасходы.Проведен
	|";
	
	Запрос.УстановитьПараметр("НачалоПериода",		ПараметрыЗапроса.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",		ПараметрыЗапроса.КонецПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций",  ПараметрыЗапроса.МассивОрганизаций);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	Результат.Вставить("ЗаписиКнигиПродаж",   0);
	Результат.Вставить("ЗаписиКнигиПокупок",  0);
	Результат.Вставить("БлокировкиВычетаНДС", 0);
	Результат.Вставить("СписанияНДСнаРасходы", 0);
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("ЗаписиКнигиПродаж", Выборка.Количество);
	КонецЕсли;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("ЗаписиКнигиПокупок", Выборка.Количество);
	КонецЕсли;
	
	Выборка = РезультатЗапроса[2].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("БлокировкиВычетаНДС", Выборка.Количество);
	КонецЕсли;

	Выборка = РезультатЗапроса[3].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("СписанияНДСнаРасходы", Выборка.Количество);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает информацию о количестве документов по учету НДС к обработке по организации за период.
//
// Параметры:
//	ПараметрыЗапроса - Структура вида "МассивОрганизаций, НачалоПериода, КонецПериода"
//
// Возвращаемое значение:
//	Структура - (ЕстьНевыполненныеОперации, ЕстьОбязательныеНевыполненныеОперации), тип элементов структуры - булево 
//
Функция ОперацииПоНДСКВыполнению(ПараметрыЗапроса) Экспорт
	
	НевыполненныеОперации = 0;
	ОбязательныеНевыполненныеОперации = 0;
	
	ТолькоОбязательныеОперации = Истина;
	СтруктураВидимостиЭлементов = Неопределено;
	ТаблицаНевыполненныхОпераций = СформироватьЗапросПоДокументамКОформлению(ПараметрыЗапроса, СтруктураВидимостиЭлементов, ТолькоОбязательныеОперации);
	Для Каждого Строка Из ТаблицаНевыполненныхОпераций Цикл
		
		Если	Строка.Документ = "СчетФактураВыданный" 
			Или Строка.Документ = "СчетФактураВыданныйАванс" 
			Или Строка.Документ = "СчетФактураКомиссионеру"
			Или Строка.Документ = "ЗаявлениеОВвозеТоваров"
			Или Строка.Документ = "СчетФактураНалоговыйАгент" Тогда
			
			ОбязательныеНевыполненныеОперации = ОбязательныеНевыполненныеОперации + Строка.КоличествоКОформлению;
		КонецЕсли;
		
		НевыполненныеОперации = НевыполненныеОперации + Строка.КоличествоКОформлению;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("НевыполненныеОперации", НевыполненныеОперации);
	Результат.Вставить("ОбязательныеНевыполненныеОперации", ОбязательныеНевыполненныеОперации);
	Результат.Вставить("ЕстьОперацииКВыполнению", (НевыполненныеОперации > 0));
	Результат.Вставить("ЕстьОбязательныеОперацииКВыполнению", (ОбязательныеНевыполненныеОперации > 0));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ТекстыЗапросов

Функция ТекстЗапросаСчетФактураВыданный(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураВыданный";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	0 КАК КоличествоКОформлению,
	|	0 КАК КоличествоОформленных";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&ИмяИсточника КАК Документ,
		|	СУММА(ДанныеКОформлению.КоличествоДокументовКОформлению) КАК КоличествоКОформлению,
		|	СУММА(ДанныеКОформлению.КоличествоОформленыхДокументов) КАК КоличествоОформленных
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК КоличествоДокументовКОформлению,
		|		0 КАК КоличествоОформленыхДокументов
		|	ИЗ
		|		РегистрСведений.ТребуетсяОформлениеСчетаФактуры КАК ТребуетсяОформлениеСчетаФактуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ 
		|			РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеОснования
		|		ПО 
		|			ТребуетсяОформлениеСчетаФактуры.Организация = ДанныеОснования.Организация
		|			И ТребуетсяОформлениеСчетаФактуры.Основание = ДанныеОснования.Документ
		|	ГДЕ
		|		ЕСТЬNULL(ДанныеОснования.Дата, ДАТАВРЕМЯ(1,1,1)) <= &КонецПериода
		|		И ТребуетсяОформлениеСчетаФактуры.Организация В (&МассивОрганизаций)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0,
		|		КОЛИЧЕСТВО(СчетФактураВыданный.Ссылка)
		|	ИЗ
		|		Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|	ГДЕ
		|		НЕ СчетФактураВыданный.ПометкаУдаления
		|		И СчетФактураВыданный.Организация В (&МассивОрганизаций)
		|		И СчетФактураВыданный.Дата МЕЖДУ &НачалоПериода И &КонецПериода) КАК ДанныеКОформлению";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСчетФактураПолученный(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураПолученный";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	&КоличествоКОформлению КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураПолученный.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	НЕ СчетФактураПолученный.ПометкаУдаления
	|	И СчетФактураПолученный.Организация В (&МассивОрганизаций)
	|	И СчетФактураПолученный.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
	
		ПараметрыПоискаСчетовФактур = УчетНДС.НовыйПараметрыПоискаСчетовФактур();		

		ПараметрыПоискаСчетовФактур.НачалоПериода 	= Параметры.НачалоПериода;
		ПараметрыПоискаСчетовФактур.КонецПериода 	= Параметры.КонецПериода;
		ПараметрыПоискаСчетовФактур.Организация		= Параметры.МассивОрганизаций;
		ПараметрыПоискаСчетовФактур.Фильтр			= Неопределено;
		ПараметрыПоискаСчетовФактур.ВсеКромеФильтра	= Истина;
		ПараметрыПоискаСчетовФактур.НаличиеСчетаФактуры = Ложь;
		ПараметрыПоискаСчетовФактур.СчетФактураПроведен = Неопределено;
		ПараметрыПоискаСчетовФактур.ДатаСФНеБолее		= Неопределено;
		ПараметрыПоискаСчетовФактур.ИскатьПоОборотам	= Ложь;

		ТаблицаДокументов = УчетНДСПереопределяемый.ОпределитьНаличиеСчетовФактурПолученных(ПараметрыПоискаСчетовФактур);
		КоличествоДокументов = ТаблицаДокументов.Количество();
		
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоКОформлению", Формат(КоличествоДокументов, "ЧН=; ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСчетФактураВыданныйАванс(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураВыданныйАванс";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	&КоличествоКОформлению КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураВыданныйАванс.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураВыданныйАванс КАК СчетФактураВыданныйАванс
	|ГДЕ
	|	НЕ СчетФактураВыданныйАванс.ПометкаУдаления
	|	И СчетФактураВыданныйАванс.Организация В (&МассивОрганизаций)
	|	И СчетФактураВыданныйАванс.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		ПолученныеАвансы = Новый ТаблицаЗначений;
		ПолученныеАвансы.Колонки.Добавить("СФсформирован");
		ПолученныеАвансы.Колонки.Добавить("Контрагент");
		ПолученныеАвансы.Колонки.Добавить("Сумма");
		ПолученныеАвансы.Колонки.Добавить("СтавкаНДС");
		ПолученныеАвансы.Колонки.Добавить("СуммаНДС");
		ПолученныеАвансы.Колонки.Добавить("ДокументОснование");
		ПолученныеАвансы.Колонки.Добавить("ДатаВыписки");
		ПолученныеАвансы.Колонки.Добавить("ВалютаДокумента");
		ПолученныеАвансы.Колонки.Добавить("ВалютнаяСумма");
		ПолученныеАвансы.Колонки.Добавить("СуммаСчетаФактуры");
		ПолученныеАвансы.Колонки.Добавить("СчетФактура");
		
		ОтборАвансов = Новый Структура;
		ОтборАвансов.Вставить("НачалоПериода", Параметры.НачалоПериода);
		ОтборАвансов.Вставить("КонецПериода", Параметры.КонецПериода);
		ОтборАвансов.Вставить("РасчетныйДокумент", Неопределено);
		ОтборАвансов.Вставить("Организация", Параметры.МассивОрганизаций);
		ОтборАвансов.Вставить("СтавкаНДСПоУмолчанию", Неопределено);
		ОтборАвансов.Вставить("ПравилоОтбораАванса", 
						Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.ВсеОплаты);
	
		Документы.СчетФактураВыданныйАванс.ЗаполнитьПолученныеАвансыДляСФ(ОтборАвансов, ПолученныеАвансы);
		
		КоличествоДокументов = ПолученныеАвансы.НайтиСтроки(Новый Структура("СФсформирован", Ложь)).Количество();
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоКОформлению", Формат(КоличествоДокументов, "ЧН=; ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСчетФактураПолученныйАванс(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураПолученныйАванс";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	&КоличествоКОформлению КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураПолученныйАванс.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураПолученныйАванс КАК СчетФактураПолученныйАванс
	|ГДЕ
	|	НЕ СчетФактураПолученныйАванс.ПометкаУдаления
	|	И СчетФактураПолученныйАванс.Организация В (&МассивОрганизаций)
	|	И СчетФактураПолученныйАванс.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		ПолученныеАвансы = Документы.СчетФактураПолученныйАванс.ПолучитьВыданныеАвансы(
			Параметры.МассивОрганизаций, 
			Параметры.НачалоПериода, 
			Параметры.КонецПериода);
		
		КоличествоДокументов = ПолученныеАвансы.НайтиСтроки(Новый Структура("СчетФактура", Документы.СчетФактураПолученныйАванс.ПустаяСсылка())).Количество();
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоКОформлению", Формат(КоличествоДокументов, "ЧН=; ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСчетФактураНалоговыйАгент(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураНалоговыйАгент";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	&КоличествоКОформлению КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураНалоговыйАгент.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураНалоговыйАгент КАК СчетФактураНалоговыйАгент
	|ГДЕ
	|	НЕ СчетФактураНалоговыйАгент.ПометкаУдаления
	|	И СчетФактураНалоговыйАгент.Организация В (&МассивОрганизаций)
	|	И СчетФактураНалоговыйАгент.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ОплатыПоставщикам = Новый ТаблицаЗначений;
		ОплатыПоставщикам.Колонки.Добавить("СчетФактура");
		ОплатыПоставщикам.Колонки.Добавить("СФсформирован");
		ОплатыПоставщикам.Колонки.Добавить("СтавкаНДС");
		ОплатыПоставщикам.Колонки.Добавить("ВидАгентскогоДоговора");
		ОплатыПоставщикам.Колонки.Добавить("ЮрФизЛицо");
		ОплатыПоставщикам.Колонки.Добавить("СуммаНДС");
		ОплатыПоставщикам.Колонки.Добавить("Сумма");
		
		ОтборРасчетов = Документы.СчетФактураНалоговыйАгент.ОтборРасчетов();
		ОтборРасчетов.НачалоПериода = Параметры.НачалоПериода;
		ОтборРасчетов.КонецПериода = Параметры.КонецПериода;
		ОтборРасчетов.Организация = Параметры.МассивОрганизаций;
		
		Документы.СчетФактураНалоговыйАгент.ЗаполнитьОплатыПоставщикам(ОтборРасчетов, ОплатыПоставщикам);
		
		КоличествоДокументов = ОплатыПоставщикам.НайтиСтроки(Новый Структура("СФсформирован", Ложь)).Количество();
		
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоКОформлению", Формат(КоличествоДокументов, "ЧН=; ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСчетФактураКомитента(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураКомитента";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	0 КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураКомитента.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураКомитента КАК СчетФактураКомитента
	|ГДЕ
	|	НЕ СчетФактураКомитента.ПометкаУдаления
	|	И СчетФактураКомитента.Организация В (&МассивОрганизаций)
	|	И СчетФактураКомитента.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ТекстЗапроса = РазделительЗапросов() +
		"ВЫБРАТЬ
		|	&ИмяИсточника КАК Документ,
		|	СУММА(Подзапрос.КоличествоКОформлению),
		|	СУММА(Подзапрос.КоличествоОформленных)
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(КРегистрации.СчетФактураВыданный) КАК КоличествоКОформлению,
		|		0 КАК КоличествоОформленных
		|	ИЗ
		|		РегистрСведений.СчетаФактурыКомитентовКРегистрации КАК КРегистрации
		|	ГДЕ
		|		КРегистрации.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И КРегистрации.Организация В (&МассивОрганизаций)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0,
		|		КОЛИЧЕСТВО(СчетФактураКомитента.Ссылка)
		|	ИЗ
		|		Документ.СчетФактураКомитента КАК СчетФактураКомитента
		|	ГДЕ
		|		СчетФактураКомитента.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И СчетФактураКомитента.Организация В (&МассивОрганизаций)
		|		И НЕ СчетФактураКомитента.ПометкаУдаления) КАК Подзапрос";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСчетФактураКомиссионеру(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "СчетФактураКомиссионеру";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	0 КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактураКомиссионеру.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураКомиссионеру КАК СчетФактураКомиссионеру
	|ГДЕ
	|	НЕ СчетФактураКомиссионеру.ПометкаУдаления
	|	И СчетФактураКомиссионеру.Организация В (&МассивОрганизаций)
	|	И СчетФактураКомиссионеру.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ТекстЗапроса = РазделительЗапросов() +
		"ВЫБРАТЬ
		|	&ИмяИсточника КАК Документ,
		|	СУММА(Подзапрос.КоличествоКОформлению),
		|	СУММА(Подзапрос.КоличествоОформленных)
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(КОформлению.ОтчетКомиссионера) КАК КоличествоКОформлению,
		|		0 КАК КоличествоОформленных
		|	ИЗ
		|		РегистрСведений.СчетаФактурыКомиссионерамКОформлению КАК КОформлению
		|	ГДЕ
		|		КОформлению.ДатаСчетаФактуры МЕЖДУ &НачалоПериода И &КонецПериода
		|		И КОформлению.Организация В (&МассивОрганизаций)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0,
		|		КОЛИЧЕСТВО(СчетФактураКомиссионеру.Ссылка)
		|	ИЗ
		|		Документ.СчетФактураКомиссионеру КАК СчетФактураКомиссионеру
		|	ГДЕ
		|		СчетФактураКомиссионеру.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И СчетФактураКомиссионеру.Организация В (&МассивОрганизаций)
		|		И НЕ СчетФактураКомиссионеру.ПометкаУдаления) КАК Подзапрос";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗаявлениеОВвозеТоваров(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "ЗаявлениеОВвозеТоваров";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК Документ,
	|	0 КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(ЗаявлениеОВвозеТоваров.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
	|ГДЕ
	|	НЕ ЗаявлениеОВвозеТоваров.ПометкаУдаления
	|	И ЗаявлениеОВвозеТоваров.Организация В (&МассивОрганизаций)
	|	И ЗаявлениеОВвозеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ТекстЗапроса = РазделительЗапросов() +
		"ВЫБРАТЬ
		|	&ИмяИсточника КАК Документ,
		|	СУММА(Подзапрос.КоличествоКОформлению),
		|	СУММА(Подзапрос.КоличествоОформленных)
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументПриобретениеТоваровУслуг.Ссылка) КАК КоличествоКОформлению,
		|		0 КАК КоличествоОформленных
		|	ИЗ
		|		Документ.ПриобретениеТоваровУслуг КАК ДокументПриобретениеТоваровУслуг
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОформлениюЗаявленийОВвозе.Остатки(&КонецПериодаГраница) КАК ТоварыКОформлениюЗаявленийОВвозеОстатки
		|			ПО ДокументПриобретениеТоваровУслуг.Ссылка = ТоварыКОформлениюЗаявленийОВвозеОстатки.ДокументПоступления
		|	ГДЕ
		|		ДокументПриобретениеТоваровУслуг.Организация В (&МассивОрганизаций)
		|		И ДокументПриобретениеТоваровУслуг.Дата <= &КонецПериода 
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0,
		|		КОЛИЧЕСТВО(ЗаявлениеОВвозеТоваров.Ссылка)
		|	ИЗ
		|		Документ.ЗаявлениеОВвозеТоваров КАК ЗаявлениеОВвозеТоваров
		|	ГДЕ
		|		ЗаявлениеОВвозеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|		И ЗаявлениеОВвозеТоваров.Организация В (&МассивОрганизаций)
		|		И НЕ ЗаявлениеОВвозеТоваров.ПометкаУдаления) КАК Подзапрос";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции


Функция ТекстЗапросаНДССостояниеРеализации0(СтруктураВидимостиЭлементов, Параметры)
	
	ИмяИсточника = "НДССостояниеРеализации0";
	
	ТекстЗапроса = РазделительЗапросов() +
	"ВЫБРАТЬ
	|	&ИмяИсточника КАК НаименованиеИсточника,
	|	0 КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(НДССостояниеРеализации0.ДокументРеализации) КАК КоличествоОформленных
	|ИЗ
	|	РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|ГДЕ
	|	НДССостояниеРеализации0.Организация В (&МассивОрганизаций)
	|	И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		ТекстЗапроса = РазделительЗапросов() +
		"ВЫБРАТЬ
		|	&ИмяИсточника КАК НаименованиеИсточника,
		|	СУММА(Подзапрос.КоличествоКОформлению),
		|	СУММА(Подзапрос.КоличествоОформленных)
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(НДССостояниеРеализации0.ДокументРеализации) КАК КоличествоКОформлению,
		|		0 КАК КоличествоОформленных
		|	ИЗ
		|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
		|	ГДЕ
		|		НДССостояниеРеализации0.Организация В (&МассивОрганизаций)
		|		И НДССостояниеРеализации0.ДатаРеализации <= &КонецПериода
		|		И НДССостояниеРеализации0.Состояние = ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ОжидаетсяПодтверждение)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОЛИЧЕСТВО(НДССостояниеРеализации0.ДокументРеализации) КАК КоличествоКОформлению,
		|		0 КАК КоличествоОформленных
		|	ИЗ
		|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
		|	ГДЕ
		|		НДССостояниеРеализации0.Организация В (&МассивОрганизаций)
		|		И НДССостояниеРеализации0.ДатаРеализации <= &КонецПериода
		|		И НДССостояниеРеализации0.ДатаПодтверждения > &КонецПериода
		|		И НДССостояниеРеализации0.Состояние В (
		|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0),
		|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		0 КАК КоличествоКОформлению,
		|		КОЛИЧЕСТВО(НДССостояниеРеализации0.ДокументРеализации) КАК КоличествоОформленных
		|	ИЗ
		|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
		|	ГДЕ
		|		НДССостояниеРеализации0.Организация В (&МассивОрганизаций)
		|		И НДССостояниеРеализации0.ДатаПодтверждения МЕЖДУ &НачалоПериода И &КонецПериода
		|		И НДССостояниеРеализации0.Состояние В (
		|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0),
		|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0))
		|	) КАК Подзапрос";
	
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяИсточника", """" + ИмяИсточника + """");
	
	Возврат ТекстЗапроса;

КонецФункции

Функция РазделительЗапросов()
	Возврат 
		"
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|";
КонецФункции

#КонецОбласти
	
#Область Прочее

Функция ВидимостьЭлементов(Организация, Период) Экспорт
	
	ПрименяетсяУСН = УчетнаяПолитика.ПрименяетсяУСН(Организация, Период);
	
	СтруктураВидимостиЭлементов = Новый Структура;
	СтруктураВидимостиЭлементов.Вставить("СчетФактураВыданный",			Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураПолученный",		Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураВыданныйАванс",	Не ПрименяетсяУСН);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураПолученныйАванс",	НЕ ПрименяетсяУСН);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураНалоговыйАгент",	Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураКомитента",		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках"));
	СтруктураВидимостиЭлементов.Вставить("СчетФактураКомиссионеру",		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах"));
	СтруктураВидимостиЭлементов.Вставить("ЗаявлениеОВвозеТоваров",		ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки"));
	СтруктураВидимостиЭлементов.Вставить("ПодтвержденияОплатыВБюджет",	ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки"));
	
	СтруктураВидимостиЭлементов.Вставить("ФормированиеЗаписейРаздела7",			ПолучитьФункциональнуюОпцию("ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС"));
	СтруктураВидимостиЭлементов.Вставить("РеестрДокументовПодтверждающихЛьготу",ПолучитьФункциональнуюОпцию("ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС"));
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		СтруктураВидимостиЭлементов.Вставить("ТаможеннаяДекларацияЭкспорт", Ложь);
	КонецЕсли;
	
	СтруктураВидимостиЭлементов.Вставить("НДССостояниеРеализации0",		ПолучитьФункциональнуюОпцию("ИспользоватьНДС0"));
	
	Возврат СтруктураВидимостиЭлементов;
	
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеДокументыПродажи

Функция СформироватьГиперссылкуСмТакжеВРаботе(Параметры) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Обработки.ПомощникПоУчетуНДС) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстГиперссылки = НСтр("ru = 'Помощник по учету НДС'");
	
	Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяФормыРабочееМесто());
	
КонецФункции

Функция ИмяФормыРабочееМесто() Экспорт
	
	Возврат "Обработка.ПомощникПоУчетуНДС.Форма.Форма";
	
КонецФункции

#КонецОбласти

#КонецЕсли
