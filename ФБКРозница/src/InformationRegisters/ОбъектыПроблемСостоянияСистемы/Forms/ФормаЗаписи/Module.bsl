
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Проверка", 		   Запись.Проверка);
	Запрос.УстановитьПараметр("Организация", 	   Запись.Организация);
	Запрос.УстановитьПараметр("ПроверяемыйПериод", Запись.ПроверяемыйПериод);
	Запрос.УстановитьПараметр("Проблема", 		   Запись.Проблема);
	Запрос.УстановитьПараметр("СоставнойОбъект", Запись.СоставнойОбъект);
	
	// Заполнение представления проблемы.
	Если ЗначениеЗаполнено(Запись.Представление) Тогда
		
		ПредставлениеПроблемы = Запись.Представление;
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПроблемыСостоянияСистемы.Представление
		|ИЗ
		|	РегистрСведений.ПроблемыСостоянияСистемы КАК ПроблемыСостоянияСистемы
		|ГДЕ
		|	ПроблемыСостоянияСистемы.Проблема = &Проблема";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			ПредставлениеПроблемы = Выборка.Представление;
		Иначе
			ПредставлениеПроблемы = НСтр("ru='не удалось нийти описание проблемы'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Объект) Тогда
		
		Элементы.Объект.Видимость = Истина;
		Элементы.Расшифровка.Видимость = Ложь;
		
	Иначе
		
		Элементы.Объект.Видимость = Ложь;
		Элементы.Расшифровка.Видимость = Истина;
		
		// Заполнение расшифровки.
		Расшифровка.Очистить();
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОбъектыПроблемСостоянияСистемы.РасшифровкаСоставногоОбъекта
		|ИЗ
		|	РегистрСведений.ОбъектыПроблемСостоянияСистемы КАК ОбъектыПроблемСостоянияСистемы
		|ГДЕ
		|	ОбъектыПроблемСостоянияСистемы.Проблема = &Проблема
		|	И ОбъектыПроблемСостоянияСистемы.СоставнойОбъект = &СоставнойОбъект";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ДопИнформация = Выборка.РасшифровкаСоставногоОбъекта.Получить();
			
			Если ТипЗнч(ДопИнформация) = Тип("Структура") ИЛИ ТипЗнч(ДопИнформация) = Тип("ФиксированнаяСтруктура") Тогда
				
				ТипВсеСсылки = ОбщегоНазначения.ОписаниеТипаВсеСсылки();
				
				Для Каждого КлючИЗначение Из ДопИнформация Цикл
					
					Если ЗначениеЗаполнено(КлючИЗначение.Значение) И ТипВсеСсылки.СодержитТип(ТипЗнч(КлючИЗначение.Значение)) Тогда
						Расшифровка.Добавить(КлючИЗначение.Значение, КлючИЗначение.Ключ + ": " + КлючИЗначение.Значение);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Получение даты последней проверки.
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыполнениеПроверокСостоянияСистемы.ДатаВыполнения
	|ИЗ
	|	РегистрСведений.ВыполнениеПроверокСостоянияСистемы КАК ВыполнениеПроверокСостоянияСистемы
	|ГДЕ
	|	ВыполнениеПроверокСостоянияСистемы.Проверка = &Проверка
	|	И ВыполнениеПроверокСостоянияСистемы.Организация = &Организация
	|	И ВыполнениеПроверокСостоянияСистемы.ПроверяемыйПериод = &ПроверяемыйПериод";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ДатаПроверки = Формат(Выборка.ДатаВыполнения, "ДЛФ=DT");
	Иначе
		ДатаПроверки = НСтр("ru='не удалось определить дату проверки'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(, Расшифровка[ВыбраннаяСтрока].Значение);
	
КонецПроцедуры

#КонецОбласти
