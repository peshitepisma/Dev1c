
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура")
		И Параметры.Свойство("Характеристика") Тогда
		УстановитьОтборПоНоменклатуре();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	Если ИмяСобытия = "Запись_Справка1" И ТипЗнч(Параметр) = Тип("СправочникСсылка.Справки1ЕГАИС") Тогда
		Элементы.Список.ТекущаяСтрока = Параметр;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтборПоНоменклатуре()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.Характеристика = &Характеристика");
	
	Запрос.УстановитьПараметр("Номенклатура",   Параметры.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Параметры.Характеристика);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"АлкогольнаяПродукция",
			Выборка.АлкогольнаяПродукция,
			ВидСравненияКомпоновкиДанных.Равно,,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
		Элементы.НадписьОтбораПоНоменклатуре.Видимость = Истина;
		СтрокаОтбораПоНоменклатуре = НСтр("ru = 'Соответствующие номенклатуре: %1'");
		Если ЗначениеЗаполнено(Параметры.Характеристика) Тогда
			ПредставлениеНоменклатуры = Параметры.Номенклатура + " / " + Параметры.Характеристика;
		Иначе
			ПредставлениеНоменклатуры = Параметры.Номенклатура;
		КонецЕсли;
		
		НадписьОтбораПоНоменклатуре = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СтрокаОтбораПоНоменклатуре,
			ПредставлениеНоменклатуры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

