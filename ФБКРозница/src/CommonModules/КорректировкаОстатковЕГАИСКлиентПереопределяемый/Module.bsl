
#Область ПрограммныйИнтерфейс

// В процедуре требуется реализовать открытие формы нового документа пересчета товаров.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - организация предприятия,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура СоздатьПриказНаПроведениеИнвентаризации(Форма, Организация, ТорговыйОбъект) Экспорт
	
	//++ НЕ ЕГАИС
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Склад", ТорговыйОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ПересчетТоваров.ФормаОбъекта",
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	//-- НЕ ЕГАИС
	
	Возврат;
	
КонецПроцедуры

// В процедуре требуется реализовать открытие формы списка документов пересчета товаров.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  Организация - ОпределяемыйТип.ОрганизацияКонтрагентЕГАИС - организация предприятия,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура ОткрытьСписокИнвентаризаций(Форма, Организация, ТорговыйОбъект) Экспорт
	
	//++ НЕ ЕГАИС
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Склад", ТорговыйОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	ОткрытьФорму("Документ.ПересчетТоваров.ФормаСписка",
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	//-- НЕ ЕГАИС
	
	Возврат;
	
КонецПроцедуры

// Формирует отчет по излишкам/недостачам для переданного торгового объекта.
//
// Параметры:
//  Форма - УправляемаяФорма - форма запущенной обработки корректировки остатков,
//  ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектЕГАИС - магазин, в котором корректируются остатки.
//
Процедура СформироватьОтчетПоИзлишкамНедостачам(Форма, ТорговыйОбъект) Экспорт
	
	//++ НЕ ЕГАИС
	Отбор = Новый Структура("Склад", ТорговыйОбъект);
	
	ПараметрыФормы = Новый Структура(
		"Отбор, КлючНазначенияИспользования, КлючВарианта, СформироватьПриОткрытии",
		Отбор,
		"ОформлениеИзлишковНедостачКонтекст",
		"ОформлениеИзлишковНедостачКонтекст",
		Истина);
	
	ОткрытьФорму("Отчет.ОформлениеИзлишковНедостачТоваров.Форма",
		ПараметрыФормы,
		Форма,
		Форма.УникальныйИдентификатор);
	//-- НЕ ЕГАИС
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти