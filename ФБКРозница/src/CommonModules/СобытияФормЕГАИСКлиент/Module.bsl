
#Область ПрограммныйИнтерфейс

// Возвращает структуру параметров заполнения табличной части.
// 
Функция СтруктураПараметрыЗаполнения() Экспорт

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПересчитатьСумму",                   Ложь);
	ПараметрыЗаполнения.Вставить("ПересчитатьКоличествоЕдиниц",        Ложь);
	ПараметрыЗаполнения.Вставить("ЗаполнитьИндексАкцизнойМарки",       Ложь);
	ПараметрыЗаполнения.Вставить("МаркируемаяАлкогольнаяПродукцияВТЧ", Ложь);
	ПараметрыЗаполнения.Вставить("ШтрихкодыВТЧ",                       Ложь);
	ПараметрыЗаполнения.Вставить("ПерезаполнитьНоменклатуруЕГАИС",     Ложь);
	ПараметрыЗаполнения.Вставить("ОбработатьУпаковки",                 Истина);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает структуру оповещений, вызываемых при сканировании штрихкода.
//
Функция СтруктураОповещенийВнешнегоСобытия(Форма, ОбработчикАкцизнойМарки = Истина, ОбработчикDataMatrix = Истина, ДополнительныеПараметры = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	Если ОбработчикАкцизнойМарки Тогда
		Результат.Вставить("ПриСканированииАкцизнойМарки", Новый ОписаниеОповещения("Подключаемый_ПолученаАкцизнаяМарка", Форма, ДополнительныеПараметры));
	КонецЕсли;
	
	Если ОбработчикDataMatrix Тогда
		Результат.Вставить("ПриСканированииDataMatrix", Новый ОписаниеОповещения("Подключаемый_ПолученDataMatrix", Форма, ДополнительныеПараметры));
	КонецЕсли;
	
	Результат.Вставить("ПриСканированииШтрихкода", Новый ОписаниеОповещения("Подключаемый_ПолученыШтрихкоды", Форма));
	
	Возврат Результат;
	
КонецФункции

// Вызывается при сканировании штрихкода в форме объекта.
//
// Параметры:
//  ОповещенияПриЗавершении - Структура - структура с ключами:
//   * ПриСканированииШтрихкода - ОписаниеОповещения -  процедура, вызываемая при сканировании любого штрихкода, не нанесенного на акцизную марку,
//   * ПриСканированииАкцизнойМарки - ОписаниеОповещения - процедура, вызываемая при сканировании штрихкода PDF417 акцизной марки,
//   * ПриСканированииDataMatrix - ОписаниеОповещения - процедура, вызываемая при сканировании штрихкода Data Matrix акцизной марки.
//  Форма - УправляемаяФорма - форма, в которой отсканирован штрихкод,
//  Источник - Строка - источник внешнего события,
//  Событие - Строка - наименование события,
//  Данные - Строка - данные для события,
//  ПараметрыСканированияАкцизныхМарок - Структура - параметры обработки акцизной марки (см. АкцизныеМаркиКлиентСервер.ПараметрыСканированияАкцизныхМарок()).
//
Процедура ВнешнееСобытиеПолученыШтрихкоды(ОповещенияПриЗавершении, Форма, Источник, Событие, Данные,
	ПараметрыСканированияАкцизныхМарок = Неопределено) Экспорт
	
	Если НЕ Форма.ВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = Новый Структура;
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие" , Событие);
	ОписаниеСобытия.Вставить("Данные"  , Данные);
	
	Результат = МенеджерОборудованияКлиент.ПолучитьСобытиеОтУстройства(ОписаниеСобытия);
	
	Если Результат <> Неопределено
		И Результат.Источник = "ПодключаемоеОборудование"
		И Результат.ИмяСобытия = "ScanData"
		И Найти(Форма.ПоддерживаемыеТипыПодключаемогоОборудования, "СканерШтрихкода") > 0 Тогда
		
		ДанныеШтрихкода = ПреобразоватьДанныеСоСканераВСтруктуру(Результат.Параметр);
		
		АкцизныеМаркиЕГАИСКлиент.ОбработатьДанныеШтрихкода(ОповещенияПриЗавершении, Форма, ДанныеШтрихкода, ПараметрыСканированияАкцизныхМарок);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПреобразоватьДанныеСоСканераВСтруктуру(Параметр)
	
	Если Параметр[1] = Неопределено Тогда
		Данные = Новый Структура("Штрихкод, Количество", Параметр[0], 1);    // Достаем штрихкод из основных данных
	Иначе
		Данные = Новый Структура("Штрихкод, Количество", Параметр[1][1], 1); // Достаем штрихкод из дополнительных данных
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти
